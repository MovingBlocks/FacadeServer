// Dependencies needed for what our Gradle scripts themselves use. It cannot be included via an external Gradle file :-(
buildscript {
    repositories {
        // External libs - jcenter is Bintray and is supposed to be a superset of Maven Central, but do both just in case
        jcenter()
        mavenCentral()
    }

    dependencies {
        // Artifactory plugin
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.0.0')
    }
}

plugins {
    id "java"
    id "org.standardout.versioneye" version "1.3.0"
    id "application"
}

apply from: "$rootDir/config/gradle/publish.gradle"
apply from: 'config/gradle/versioning.gradle'

import groovy.json.JsonBuilder

mainClassName = "org.terasology.web.ServerMain"

ext {
    localServerDataPath = 'terasology-server'
}

// For artifact organizing - version is set in gradle.properties
group = 'org.terasology.web'

def jettyVersion = '9.3.8.v20160314'
def jerseyVersion = '2.22.2'

configurations {
    modules
}

dependencies {

    implementation group: 'org.terasology.engine', name: 'engine', version: '+', changing: true

    //TODO: add Guava dependency?

    implementation group: 'com.google.code.gson', name: 'gson', version: '2.6.2'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jettyVersion
    implementation group: 'org.eclipse.jetty.websocket', name: 'websocket-server', version: jettyVersion

    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    //TODO: update this to newer version if the engine updates its version of JNA
    implementation group: 'com.github.oshi', name: 'oshi-core', version: '3.4.0'

    implementation group: 'org.glassfish.jersey.containers', name: 'jersey-container-jetty-servlet', version: jerseyVersion
    implementation group: 'org.glassfish.jersey.ext', name: 'jersey-mvc-freemarker', version: jerseyVersion

    testImplementation 'junit:junit:4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.0'
}

jar {
    manifest {
        attributes('Main-Class': mainClassName)
        attributes('Class-Path': "jopenvr.jar " + configurations.runtimeClasspath.collect { it.getName() }.join(' '))
    }
}

// Copied from PC facade
// By delaying this task to doLast we don't get the headless server dir set up unless actually wanting it
task setupServerConfig() {
    description "Parses parameters passed via Gradle and writes them to the local run-from-source server dir's config.cfg"

    def serverRoot = rootProject.file(localServerDataPath);
    def config = new File(serverRoot, 'config.cfg')
    onlyIf { !config.exists() }

    doLast {
        def json = new JsonBuilder()

        serverRoot.mkdir()
        logger.lifecycle("Creating config file $config")

        json {
            worldGeneration {
                if (project.hasProperty('seed')) {
                    logger.lifecycle("  Seed value: $seed");
                    defaultSeed seed
                }
                if (project.hasProperty('worldGen')) {
                    logger.lifecycle("  World Generator: $worldGen");
                    defaultGenerator worldGen
                }
            }
            defaultModSelection {
                if (project.hasProperty('extraModules')) {
                    logger.lifecycle("  Enabling modules: $extraModules");
                    modules extraModules.tokenize(" ,")
                }
            }
        }
        config.text = json.toPrettyString()
    }
}

// TODO: Seems to always be up to date so no modules get copied
task setupServerModules(type: Sync) {
    description 'Parses "extraModules" - a comma-separated list of modules and puts them into ' + localServerDataPath

    if (project.hasProperty('extraModules')) {
        // Grab modules from Artifactory - cheats by declaring them as dependencies
        extraModules.tokenize(' ,').each { String module ->
            println "Extra module: " + module
            dependencies {
                modules group: 'org.terasology.modules', name: module, version: '+', changing: 'true'
            }
        }
    }

    from(configurations.modules)
    into(new File(rootProject.file(localServerDataPath), "modules"))
}

run.workingDir = rootDir
run.args = ["-homedir=terasology-server"]
run.dependsOn setupServerConfig
run.dependsOn setupServerModules

// Support both standalone workspace (expect certain things provided) and embedding in a Terasology workspace (use task dependencies)
if (project.name != project(':').name) {
    run.dependsOn rootProject.extractNatives
    run.dependsOn rootProject.moduleClasses
} else {
    // For a standalone workspace natives should be provided
}
